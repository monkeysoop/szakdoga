#version 430 core

layout (local_size_x = 8, local_size_y = 8) in;
layout (rg32f, binding = 0) uniform readonly image2D distancesIterationsTexture1;
layout (rg32f, binding = 1) uniform writeonly image2D distancesIterationsTexture2;
layout (rgba32f, binding = 2) uniform readonly image2D precomputedConesTexture;


uniform mat4 u_inv_view_mat;
uniform vec3 u_position;
uniform uint u_width;
uniform uint u_height;

uniform float u_time_in_seconds;
uniform float u_epsilon;
uniform float u_max_distance;
uniform float u_max_iteration_count;

uniform float u_relaxed_step_multiplier;
uniform float u_enhanced_step_multiplier;
uniform float u_enhanced_max_step_factor;

uniform bool u_first_pass;


#define NEWTONS_CRADLE 0
#define CAR 1
#define TEMPLE 2

#define NAIVE 0
#define RELAXED 1
#define ENHANCED 2

#define SDF_SCENE @SDF_SCENE@
#define SPHERE_TRACING_TYPE @SPHERE_TRACING_TYPE@


#include "assets/sdf.include"
#include "assets/value.include"
#include "assets/ray.include"



vec2 naive_cone_tracing(Ray ray, float initial_t, float initial_iteration, float cone_scale) {
    float d;
    float t = initial_t;

    float steps = initial_iteration;

    float step_size;

    do {
        d = abs(sdf(ray.position + t * ray.direction).d);
        steps += 1.0;

        step_size = d - cone_scale * t;
        t += max(step_size, 0.0);
    } while (step_size >= u_epsilon && t < u_max_distance && steps < u_max_iteration_count);

    if (step_size < u_epsilon) {
        return vec2(t, steps);
    } else if (t >= u_max_distance) {
        return vec2(u_max_distance, steps);
    } else {
        return vec2(t, u_max_iteration_count);
    }
}

vec2 relaxed_cone_tracing(Ray ray, float initial_t, float initial_iteration, float cone_scale) {
    float d = 0.0;
    float t = initial_t;

    float next_d;
    float next_t = initial_t;

    float steps = initial_iteration;

    float step_size = 0.0; // not necesseary to be initialized

    do {
        next_d = abs(sdf(ray.position + next_t * ray.direction).d);
        steps += 1.0;

        if (t != next_t && (d + next_d) < (step_size + (t + d) * cone_scale)) {
            step_size = d - cone_scale * t;
            next_t = t + max(step_size, 0.0);
            
            t = next_t;
        } else {
            t = next_t;
            d = next_d;

            step_size = u_relaxed_step_multiplier * next_d - u_relaxed_step_multiplier * next_t * cone_scale;
            next_t += max(step_size, 0.0);
        }
    } while (step_size >= u_epsilon && t < u_max_distance && steps < u_max_iteration_count);

    if (step_size < u_epsilon) {
        return vec2(t, steps);
    } else if (t >= u_max_distance) {
        return vec2(u_max_distance, steps);
    } else {
        return vec2(t, u_max_iteration_count);
    }
}

vec2 enhanced_cone_tracing(Ray ray, float initial_t, float initial_iteration, float cone_scale) {
    float d = 0.0;
    float t = initial_t;

    float next_d;
    float next_t = initial_t;

    float steps = initial_iteration;

    float prev_step_size = 0.0;
    float step_size = 0.0;

    do {
        next_d = abs(sdf(ray.position + next_t * ray.direction).d);
        steps += 1.0;

        if (t != next_t && (d + next_d) < (step_size + (t + d) * cone_scale)) {
            step_size = d - cone_scale * t;
            next_t = t + max(step_size, 0.0);

            prev_step_size = step_size;
            t = next_t;
        } else {
            float factor = 1.0 + u_enhanced_step_multiplier * ((-1.0 * d + next_d + prev_step_size) / (d - next_d + prev_step_size));
            factor = clamp(factor, 1.0, u_enhanced_max_step_factor);

            t = next_t;
            d = next_d;

            prev_step_size = step_size;

            step_size = factor * next_d - factor * next_t * cone_scale;
            next_t += max(step_size, 0.0);
        }
    } while (step_size >= u_epsilon && t < u_max_distance && steps < u_max_iteration_count);

    if (step_size < u_epsilon) {
        return vec2(t, steps);
    } else if (t >= u_max_distance) {
        return vec2(u_max_distance, steps);
    } else {
        return vec2(t, u_max_iteration_count);
    }
}

vec2 ConeTracing(Ray ray, float initial_t, float initial_iteration, float cone_scale) {
#if SPHERE_TRACING_TYPE == NAIVE
    return naive_cone_tracing(ray, initial_t, initial_iteration, cone_scale);
#elif SPHERE_TRACING_TYPE == RELAXED
    return relaxed_cone_tracing(ray, initial_t, initial_iteration, cone_scale);
#elif SPHERE_TRACING_TYPE == ENHANCED
    return enhanced_cone_tracing(ray, initial_t, initial_iteration, cone_scale);
#endif
}

void main() {
    if (gl_GlobalInvocationID.x >= u_width || gl_GlobalInvocationID.y >= u_height) {
        return;
    }

    vec4 precomputed = imageLoad(precomputedConesTexture, ivec2(gl_GlobalInvocationID.xy));

    vec4 ray_target = u_inv_view_mat * vec4(precomputed.xyz, 1.0);
    vec3 ray_dir = normalize(ray_target.xyz - u_position);
    
    float cone_scale = precomputed.w;

    float initial_t = 0.0;
    float initial_iteration = 0.0;
    if (!u_first_pass) {
        vec4 initial = imageLoad(distancesIterationsTexture1, ivec2(gl_GlobalInvocationID.xy / 2));

        initial_t = initial.r;
        initial_iteration = initial.g * 0.25;

        if (initial_t >= u_max_distance || initial_iteration >= u_max_iteration_count) {
            imageStore(distancesIterationsTexture2, ivec2(gl_GlobalInvocationID.xy), vec4(initial_t, initial_iteration, 0.0, 0.0));
            return;
        }
    }

    vec2 res = ConeTracing(Ray(u_position, ray_dir), initial_t, initial_iteration, cone_scale);

    imageStore(distancesIterationsTexture2, ivec2(gl_GlobalInvocationID.xy), vec4(res.x, res.y, 0.0, 0.0));
}